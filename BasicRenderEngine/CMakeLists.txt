cmake_minimum_required (VERSION 3.1 FATAL_ERROR)
project (BasicRenderEngine)

set(Boost_USE_STATIC_LIBS OFF)
set(Boost_USE_MULTITHREADED ON)
find_package(Boost 1.56 REQUIRED system)
find_package(OpenGL REQUIRED)
find_package(GLEW REQUIRED)
find_package(glfw3 REQUIRED)
find_package(glm)
if(GLM_FOUND)
    set(GLM_INCLUDE ${GLM_INCLUDE_DIRS})
else()
    set(GLM_INCLUDE "" CACHE PATH "GLM include directory")
    if(NOT EXISTS "${GLM_INCLUDE}/glm/glm.hpp")
        message(SEND_ERROR "Need valid GLM include directory!")
    endif()
endif(GLM_FOUND)
include_directories ("include" ${OPENGL_INCLUDE_DIRS} ${Boost_INCLUDE_DIRS} ${GLEW_INCLUDE_DIRS} ${GLFW3_INCLUDE_DIR} ${GLM_INCLUDE})

add_subdirectory(src)

add_executable(BasicRenderEngine ${SOURCES})
set_property(TARGET BasicRenderEngine PROPERTY CXX_STANDARD 11)
set_property(TARGET BasicRenderEngine PROPERTY CXX_STANDARD_REQUIRED ON)
target_link_libraries(BasicRenderEngine debug ${Boost_SYSTEM_LIBRARY_DEBUG})
target_link_libraries(BasicRenderEngine optimized ${Boost_SYSTEM_LIBRARY_RELEASE})
target_link_libraries(BasicRenderEngine general ${GLEW_LIBRARY} ${GLFW3_LIBRARY} ${OPENGL_gl_LIBRARY})

install(TARGETS BasicRenderEngine DESTINATION bin)
install(DIRECTORY resources DESTINATION "${CMAKE_INSTALL_PREFIX}")